repositories {
    mavenLocal()

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven { url 'https://jitpack.io' }

    maven {
        url 'https://repo.codemc.org/repository/maven-public/'
    }

    maven {
        url 'https://repo.mattstudios.me/artifactory/public/'
    }

    maven {
        name 'TimeAPI'
        url 'https://repo.piggypiglet.me/releases'
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    // Actions
    compileOnly 'me.gabytm.util:actions-core:1.0.0-SNAPSHOT'
    implementation('me.gabytm.util:actions-spigot:1.0.0-SNAPSHOT') { exclude group: 'com.google.guava' }

    compileOnly('com.github.MilkBowl:VaultAPI:1.7') { transitive false }

    // NBT
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.8.0'

    // Adventure
    implementation 'net.kyori:adventure-api:4.9.3'
    implementation 'net.kyori:adventure-platform-bukkit:4.0.1'
    implementation 'net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT'

    // GUis and commands
    implementation 'dev.triumphteam:triumph-gui:3.1.0-SNAPSHOT'
    implementation 'me.mattstudios.utils:matt-framework:1.4.6'

    // Heads
    compileOnly('com.arcaniax:HeadDatabase-API:1.3.1') { transitive false }

    // WorldGuard compatibility
    implementation project(':worldguard_compat')
    implementation project(':worldguard_compat_v6')
    implementation project(':worldguard_compat_v7')

    // bStats
    implementation 'org.bstats:bstats-bukkit:2.2.1'

    // TimeAPI - https://github.com/PiggyPiglet/TimeAPI
    implementation 'me.piggypiglet:TimeAPI:+'

    // Other dependencies that don't have a repository or the release is old
    compileOnly fileTree(dir: './libs', includes: ['*.jar'])
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

processResources {
    eachFile { expand version: project.version }
}

shadowJar {
    archiveName("ArcaneVouchers [${project.version}].jar")

    [
            'me.gabytm.util.actions': 'actions',
            'dev.triumphteam.gui'   : 'gui',
            'net.kyori.adventure'   : 'adventure',
            'me.mattstudios.mf'     : 'commands',
            'sh.okx.timeapi'        : 'timeapi',
            'org.bstats'            : 'bstats',
            'kotlin'                : 'kt'
    ].each { relocate(it.key, project.group + '.arcanevouchers.libs.' + it.value) }
}

task copyToServer(type: Copy) {
    if (testServerVersion == '') {
        from shadowJar
        into '../server/plugins'
    } else {
        from shadowJar
        into '../server/.' + testServerVersion + '/plugins'
    }
}